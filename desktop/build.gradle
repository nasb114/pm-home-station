group 'pmstation'
version '1.1.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'edu.sc.seis.macAppBundle'

def local_appName = 'PM Home Station'
def local_mainClassName = 'pmstation.Start'
def local_projectUrl = 'https://github.com/rjaros87/pm-home-station'

sourceCompatibility = 1.8
mainClassName = local_mainClassName

repositories {
    mavenCentral()
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

// TODO ensure fat (uber) jar is created separately (by using shadow plugin?) to avoid having redundant uberjar + libs for .zip and DMG distribution versions
jar {
    manifest {
        attributes(
                'Implementation-Title': 'PM Home Station',
                'Implementation-Version': version,
                'Main-Class': mainClassName
        )
    }
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude('META-INF/*.txt')
}

launch4j {
    mainClassName = "${local_mainClassName}"
    icon = "${projectDir}/app-icon.ico"
    outfile = "${project.name}-${version}.exe"
    supportUrl = "${local_projectUrl}"
    companyName = "${local_projectUrl}"
    version = "${version}"
    textVersion = "${version}"
    productName = "${local_appName}"
    copyright = 'Copyright (C), licence: GPL-3.0'
}

macAppBundle {
    appName = "${local_appName}"
    mainClassName = "${local_mainClassName}"
    icon = 'dmg-myIcon.icns'
    bundleJRE = false
    javaProperties.put('apple.laf.useScreenMenuBar', 'true')
    backgroundImage = 'dmg-bg.png'
    appIconX = 50
    appIconY = 120
    appFolderX = 330
    appFolderY = 120
}

dependencies {
    extraLibs group: 'org.knowm.xchart', name: 'xchart', version: '3.5.0'
    extraLibs group: 'com.fazecast', name: 'jSerialComm', version: '1.3.11'
    extraLibs group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    extraLibs group: 'com.miglayout', name: 'miglayout-swing', version: '5.0'
    extraLibs group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    extraLibs group: 'org.apache.commons', name: 'commons-configuration2', version: '2.2'
    extraLibs group: 'commons-io', name: 'commons-io', version: '2.5'
    extraLibs group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'
    extraLibs group: 'commons-cli', name: 'commons-cli', version: '1.4'
    extraLibs group: 'com.google.guava', name: 'guava', version: '23.5-jre'

    extraLibs project(':core')
    configurations.compile.extendsFrom(configurations.extraLibs)
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
